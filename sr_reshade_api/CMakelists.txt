cmake_minimum_required(VERSION 3.23)

project (srReshadeApi VERSION 0.1)
message("SR ReShade Api Version: ${srReshadeApi_VERSION}")

set(CMAKE_DEBUG_POSTFIX d)

add_library(srReshadeApi SHARED
src/srreshade.h
src/srreshade.cpp
)

target_compile_definitions(srReshadeApi PUBLIC SR_RESHADEAPI_EXPORTS)
set_target_properties(srReshadeApi PROPERTIES LINKER_LANGUAGE CXX)

install(TARGETS srReshadeApi
        EXPORT srReshadeApiTargets
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION lib
        ARCHIVE DESTINATION lib
        PUBLIC_HEADER DESTINATION include)

install(EXPORT srReshadeApiTargets
		FILE srReshadeApiTargets.cmake
		DESTINATION lib/cmake/srReshadeApi
)

target_include_directories(srReshadeApi INTERFACE
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
	$<INSTALL_INTERFACE:include>
)

include(CMakePackageConfigHelpers)
configure_package_config_file(${CMAKE_CURRENT_SOURCE_DIR}/Config.cmake.in
    ${CMAKE_CURRENT_BINARY_DIR}/srReshadeApiConfig.cmake
    INSTALL_DESTINATION lib/cmake/srReshadeApi
)

write_basic_package_version_file(
  "${CMAKE_CURRENT_BINARY_DIR}/srReshadeApiConfigVersion.cmake"
  VERSION "${srReshadeApi_VERSION}"
  COMPATIBILITY AnyNewerVersion
)

install(FILES
  ${CMAKE_CURRENT_BINARY_DIR}/srReshadeApiConfig.cmake
  ${CMAKE_CURRENT_BINARY_DIR}/srReshadeApiConfigVersion.cmake
  DESTINATION lib/cmake/srReshadeApi
)
